dnl ###
dnl ### NSC -- Makefile Builder
dnl ### (c) 1997--2011 Martin Mares <mj@ucw.cz>
dnl ###
include(m4/dnslib.m4)

# Definition of primary domains; secondaries we needn't take care of

define(`PRIMARIES', `')

define(`nsc_file_name_src', `nsc_file_name(patsubst(`$*', `.\(ip6\|in-addr\).arpa$', `'))')
define(`nsc_src_files', `nsc_foreach(`F', `($@)', ` CFDIR/nsc_file_name_src(F)')')

define(`PRIMARY', `
pushdef(`_DEPS', nsc_quote(nsc_src_files($@)))
divert(0)ZONEDIR/nsc_file_name(`$1'):dnl
 esyscmd(`echo $(m4 -di -DHASHING m4/nsc.m4 '_DEPS` 2>&1 >/dev/null \
	    | sed -n "s/^m4debug: input read from //p;");
	    echo "generating dependencies for 'ZONEDIR/nsc_file_name(`$1')`" >&2')dnl
	@bin/genzone nsc_file_name(`$1')`'_DEPS

divert(-1)
popdef(`_DEPS')
define(`PRIMARIES', PRIMARIES ZONEDIR/nsc_file_name($1))
')

define(`REVERSE', `PRIMARY(REV($1), shift($@))')

define(`BLACKHOLE', `define(`NEED_BLACKHOLE', 1)')
define(`CONFIG', `$1')	# for BLACKHOLE encapsulated in CONFIG...

# Insertion of raw makefile material

define(`MAKEFILE', `divert(0)$1
divert(-1)')

# Last words

define(`nsc_cleanup', `
ifdef(`NEED_BLACKHOLE', `PRIMARY(blackhole)')

divert(0)dnl
VERSDIR/.version: CFDIR/domains ROOTCACHE`'PRIMARIES
	NAMED_RESTART_CMD
	touch VERSDIR/.version

clean:
	find BAKDIR ZONEDIR HASHDIR -maxdepth 1 -type f | xargs rm -f

clobber: clean
	rm -f Makefile named.conf bin/genzone

distclean: clobber
	find VERSDIR -maxdepth 1 -type f | xargs rm -f
')

divert(0)dnl
`#'
`#'	Nameserver Configuration Makefile
`#'	Generated by NSCVER (mkmf.m4) on CURRENT_DATE
`#'	Please don't edit manually
`#'

all: VERSDIR/.version
m4wrap(`nsc_cleanup')
divert(-1)
